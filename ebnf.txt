program: declaration*
declaration: varDeclaration | statement
varDeclaration: "var" IDENTIFIER ("=" expression)? ";"
statement: printStatement | blockStatement | ifStatement
printStatement: "print" expression ";"
blockStatement: "{" declaration* "}"
ifStatement: "if" "(" expression ")" statement ("else" statement)?
expression: comma
comma: comma "," assignment | assignment
assignment: IDENTIFIER "=" assignment | equality
equality: comparison (("!=" | "==" ) comparison)*
comparison: term (("<" | ">" | "<=" | ">=") term)*
term: factor (("-" | "+") factor)*
factor: unary (("*" | "/") unary)*
unary: ("-" | "!") unary | primary
primary: STRING | NUMBER | "true" | "false" | "nil" | IDENTIFIER | "(" expression ")"
